Sub StockCount()
    
    '(Booth)
    Dim ws As Worksheet
    '(Booth)
    ' Loop through all of the worksheets in the active workbook.
    For Each ws In Worksheets
    
    
        'Define our Summary table columns
        ws.Cells(1, 9).Value = "Ticker"
        ws.Cells(1, 10).Value = "Yearly Change"
        ws.Cells(1, 11).Value = "Percent Change"
        ws.Cells(1, 12).Value = "Total Volume"

        'Initialize our variables
        'Had to set i and tota_volume as Double because Overflow issue when trying to get to the  non blank row
        Dim i As Double
        Dim total_volume As Double
        Dim summary_table_row As Integer
        
        ' Keep track of stock open and close
        Dim stock_open As Double
        Dim stock_close As Double
        Dim yearly_change As Double
        Dim percent_change As Double
        
        
        'Find the last non-blank cell in column A(1) (Booth)
        Dim last_row As Long
        last_row = ws.Cells(Rows.Count, 1).End(xlUp).Row
        
        
        'Initialize the total_volume
        total_volume = 0
        
        'Start the summary table row value at 2
        summary_table_row = 2
        
        'Collect The first open value of our Ticker Table
        stock_open = ws.Cells(2, 3).Value
        
        For i = 2 To last_row
        
            ' Searches for when the value of the next cell is different than that of the current cell
            If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then
            ' MsgBox ("different")
            
                'add the volume count of each row that holds our ticker
                total_volume = total_volume + ws.Cells(i, 7).Value
                
                'Collect the last close value of the ticker
                stock_close = ws.Cells(i, 6).Value
                
                yearly_change = stock_close - stock_open
                
                ' Calculate the percent change for each ticker
                ' We put it in a conditional because we wouldn't be able to divide by zero if the stock_open = 0
                If stock_open > 0 Then
                    percent_change = yearly_change / stock_open
                Else
                    percent_change = 0
                End If
            
                
                'Create our summary table
                'Write the total volume
                ws.Cells(summary_table_row, 12).Value = total_volume
                
                'Write the yearly change
                ws.Cells(summary_table_row, 10).Value = yearly_change
                
                'Write the yearly change
                ws.Cells(summary_table_row, 11).Value = percent_change
                
                'Write the Ticker Value
                ws.Cells(summary_table_row, 9).Value = ws.Cells(i, 1).Value
                
                
                'Apply our Color formatting to the yearly change column of the summary table
                If yearly_change < 0 Then
                    ws.Cells(summary_table_row, 10).Interior.ColorIndex = 3
                Else
                    ws.Cells(summary_table_row, 10).Interior.ColorIndex = 4
                End If
                
                'RESETS for the next row and/or Ticker
                
                ' Add One to Summary Table Row
                summary_table_row = summary_table_row + 1
                
                'Reset total Volume
                total_volume = 0
                
                'Reset the percentage change
                percent_change = 0
                
                'Reset total Volume to the the next tickers first open value
                stock_open = ws.Cells(i + 1, 3)
                
                'If the value in next row is the same Ticker as the the row previous
                Else
                total_volume = total_volume + ws.Cells(i, 7).Value
            
            End If
            
        
        
        Next i
        
        ' Style percent change coulumn to "%"
        ws.Range("K:K").NumberFormat = "0.00%"

    Next

End Sub